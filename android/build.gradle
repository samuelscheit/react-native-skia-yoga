import java.nio.file.Paths
import groovy.json.JsonSlurper

buildscript {
  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:8.11.1"
  }
}

def reactNativeArchitectures() {
  def value = rootProject.getProperties().get("reactNativeArchitectures")
  return value ? value.split(",") : ["armeabi-v7a", "x86", "x86_64", "arm64-v8a"]
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: "com.android.library"
apply plugin: 'org.jetbrains.kotlin.android'
apply from: '../nitrogen/generated/android/RNSkiaYoga+autolinking.gradle'
apply from: "./fix-prefab.gradle"

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

def getExtOrDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties["RNSkiaYoga_" + name]
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["RNSkiaYoga_" + name]).toInteger()
}

static def findNodeModules(baseDir) {
  def basePath = baseDir.toPath().normalize()
  // Node's module resolution algorithm searches up to the root directory,
  // after which the base path will be null
  while (basePath) {
    def nodeModulesPath = Paths.get(basePath.toString(), "node_modules")
    def reactNativePath = Paths.get(nodeModulesPath.toString(), "react-native")
    if (nodeModulesPath.toFile().exists() && reactNativePath.toFile().exists()) {
      return nodeModulesPath.toString()
    }
    basePath = basePath.getParent()
  }

}

def nodeModulesDir = findNodeModules(Paths.get(rootProject.projectDir.toString(), "..").toFile())

def reactNativeMinorVersion(nodeModulesDir) {
  def pkg = new File(nodeModulesDir, "react-native/package.json")
  if (!pkg.exists()) { return 81 }
  def json = new JsonSlurper().parseText(pkg.getText("UTF-8"))
  def ver = (json.version ?: "0.81.0") as String
  def parts = ver.tokenize(".")
  return parts.size() > 1 ? (parts[1] as int) : 81
}

println("RNSkiaYoga: Using node_modules dir: " + nodeModulesDir)

afterEvaluate {
  def skia = findProject(':react-native-skia')
  if (skia != null) {
    tasks.matching { it.name.startsWith("externalNativeBuild") || it.name.startsWith("buildCMake") }.configureEach {
      if (name.toLowerCase().contains("debug")) {
        dependsOn(":react-native-skia:assembleDebug")
      } else if (name.toLowerCase().contains("release")) {
        dependsOn(":react-native-skia:assembleRelease")
      }
    }
  }
}

android {
  namespace "com.margelo.nitro.RNSkiaYoga"

  ndkVersion getExtOrDefault("ndkVersion")
  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
    buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()

    externalNativeBuild {
      cmake {
        cppFlags "-frtti -fexceptions -Wall -Wextra -fstack-protector-all"
        arguments "-DANDROID_STL=c++_shared", "-DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=ON", "-DNODE_MODULES_DIR=${nodeModulesDir}", "-DREACT_NATIVE_DIR=${nodeModulesDir}/react-native", "-DREACT_NATIVE_VERSION=${reactNativeMinorVersion(nodeModulesDir)}", "-DANDROID_ALLOW_UNDEFINED_SYMBOLS=TRUE"
        abiFilters (*reactNativeArchitectures())

        buildTypes {
          debug {
            cppFlags "-O1 -g"
          }
          release {
            cppFlags "-O2"
          }
        }
      }
    }
  }

  externalNativeBuild {
    cmake {
      path "CMakeLists.txt"
    }
  }

  packaging {
    jniLibs {
      // Don’t ship RN Skia’s .so from this library
      excludes += ["**/librnskia.so"]
    }
  }

  packagingOptions {
    excludes = [
            "META-INF",
            "META-INF/**",
            "**/libc++_shared.so",
            "**/libfbjni.so",
            "**/libjsi.so",
            "**/libfolly_json.so",
            "**/libfolly_runtime.so",
            "**/libglog.so",
            "**/libhermes.so",
            "**/libhermes-executor-debug.so",
            "**/libhermes_executor.so",
            "**/libreactnative.so",
            "**/libreactnativejni.so",
            "**/libturbomodulejsijni.so",
            "**/libreact_nativemodule_core.so",
            "**/libjscexecutor.so"
    ]
  }

  buildFeatures {
    buildConfig true
    prefab true
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  sourceSets {
    main {
      if (isNewArchitectureEnabled()) {
        java.srcDirs += [
          // React Codegen files
          "${project.buildDir}/generated/source/codegen/java"
        ]
      }
    }
  }
}

repositories {
  mavenCentral()
  google()
}


dependencies {
  // For < 0.71, this will be from the local maven repo
  // For > 0.71, this will be replaced by `com.facebook.react:react-android:$version` by react gradle plugin
  //noinspection GradleDynamicVersion
  implementation "com.facebook.react:react-native:+"

  // Add a dependency on NitroModules
  implementation project(":react-native-nitro-modules")

  def skiaProject = findProject(':react-native-skia')
  if (skiaProject != null) {
    implementation skiaProject
  } else {
    logger.warn("react-native-skia-yoga: :react-native-skia project not found on classpath. Make sure @shopify/react-native-skia is installed.")
  }
}
